<%INIT>
my $title = loc('System Configuration');
unless ($session{'CurrentUser'}->HasRight( Object=> $RT::System, Right => 'SuperUser')) {
 Abort(loc('This feature is only available to system administrators'));
}

my @results;

my $doc_version = $RT::VERSION;
$doc_version =~ s/rc\d+//; # 4.4.2rc1 -> 4.4.2
$doc_version =~ s/\.\d+-\d+-g\w+$//;  # 4.4.3-1-g123 -> 4.4

use Data::Dumper;
sub stringify {
    my $value = shift;
    return "" if !defined($value);

    local $Data::Dumper::Terse = 1;
    local $Data::Dumper::Indent = 2;
    my $output = Dumper $value;
    chomp $output;
    return $output;
}

if (delete $ARGS{Update}) {
    $RT::Handle->BeginTransaction;
    my $has_error;

    for my $key (keys %ARGS) {
        next if $key =~ /-Current$/;

        my $meta = RT->Config->Meta( $key );

        my $val = $ARGS{$key};
        $val = '' if $val eq '__empty_value__';
        my $prev = $ARGS{$key . '-Current'};
        next if $val eq $prev;

        if ( $meta->{Immutable} || $meta->{Obfuscate} || ($key =~ /Password/i and $key !~ /MinimumPasswordLength|AllowLoginPasswordAutoComplete/ )) {
            push @results, loc("Cannot change [_1]: Permission Denied", $key);
            $has_error++;
            next;
        }

        my $setting = RT::DatabaseSetting->new($session{CurrentUser});
        $setting->Load($key);
        if ($setting->Id) {
            if ($setting->Disabled) {
                $setting->SetDisabled(0);
            }

            my ($ok, $msg) = $setting->SetContent($val);
            push @results, $msg;
            $has_error++ if !$ok;
        }
        else {
            my ($ok, $msg) = $setting->Create(
                Name    => $key,
                Content => $val,
            );
            push @results, $msg;
            $has_error++ if !$ok;
        }
    }

    if ($has_error) {
        push @results, loc("No changes made.");
        $RT::Handle->Rollback;
    }
    else {
        $RT::Handle->Commit;
    }
}

</%INIT>
<& /Admin/Elements/Header, Title => $title &>
<& /Elements/Tabs &>
<& /Elements/ListActions, actions => \@results &>

<form method="post" action="EditConfig.html">
<input type="hidden" name="Update" value=1></input>

<&|/Widgets/TitleBox, title => loc("RT Configuration") &>
<table border="0" cellspacing="0" cellpadding="5" width="100%" class="collection">
<tr class="collection-as-table">
<th class="collection-as-table"><&|/l&>Option</&></th>
<th class="collection-as-table"><&|/l&>Value</&></th>
</tr>
<%PERL>
my $index_conf;
foreach my $key ( RT->Config->Options( Overridable => undef, Sorted => 0 ) ) {
    my $meta = RT->Config->Meta( $key );

    next if $meta->{Invisible};

    my $raw_value = RT->Config->Get( $key, $session{'CurrentUser'} );
    my $val = stringify($raw_value);

    $index_conf++;

    my $doc_url = "https://docs.bestpractical.com/rt/$doc_version/RT_Config.html#$key";

</%PERL>
<tr class="<% $index_conf%2 ? 'oddline' : 'evenline'%>">
<td class="collection-as-table"><a href="<% $doc_url %>" target="_blank"><% $key %></a></td>
<td class="collection-as-table">
% if ( $meta->{EditLink} ) {
<&|/l_unsafe, "<a href=\"$meta->{EditLink}\">", loc($meta->{EditLinkLabel}), "</a>" &>Visit [_1][_2][_3] to manage this setting</&>
% } elsif ( $meta->{Immutable} || $meta->{Obfuscate} || ($key =~ /Password/i and $key !~ /MinimumPasswordLength|AllowLoginPasswordAutoComplete/ )) {
<em><% loc('Must modify in config file' ) %></em>\
% } else {
% my $widget = $meta->{'Widget'} || '/Widgets/Form/Code';
% my $args   = $meta->{'WidgetArguments'} || {};
% my $current_value = $widget eq '/Widgets/Form/Code' ? $val : $raw_value;

% if ($widget eq '/Widgets/Form/Code' && !$session{CurrentUser}->HasRight(Right => 'ExecuteCode', Object => RT->System)) {
<em><% loc('Must modify in config file' ) %></em>
% } else {
  <& $widget,
    Default      => 1,
    DefaultValue => '',
    DefaultLabel => '(no value)',

    %{ $m->comp('/Widgets/FinalizeWidgetArguments', WidgetArguments => $args ) },
    Name         => $key,
    CurrentValue => $current_value,
    Description  => '',
    Hints        => '',
  &>
<textarea class="hidden" name="<% $key %>-Current"><% $current_value %></textarea>
% }
% }
</td>
</tr>
% }
</table>
</&>
<& /Elements/Submit, Label => loc('Save Changes') &>
</form>
